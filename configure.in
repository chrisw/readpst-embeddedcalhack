AC_PREREQ(2.59)
AC_INIT(libpst,0.6.14,carl@five-ten-sg.com)
AC_CONFIG_SRCDIR([config.h.in])
AC_CONFIG_HEADER([config.h])

AM_INIT_AUTOMAKE($PACKAGE_NAME,$PACKAGE_VERSION)

# Checks for programs.
my_build_dii=yes
AC_PATH_PROG(CONVERT, convert)
if test "x$CONVERT" = "x" ; then
    AC_MSG_WARN([convert not found.  pst2dii disabled])
    my_build_dii=no
fi
AM_CONDITIONAL(BUILD_DII, test $my_build_dii = yes)

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

AC_SYS_LARGEFILE
AC_CHECK_SIZEOF(off_t)

# Checks for header files.
AC_CHECK_HEADER([unistd.h],
    AM_CONDITIONAL(NEED_XGETOPT, [test yes = no ]),
    AM_CONDITIONAL(NEED_XGETOPT, [test yes = yes])
    )
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h limits.h malloc.h netinet/in.h stdint.h stdlib.h string.h sys/param.h wchar.h])
if test "$my_build_dii" = "yes"; then
    AC_CHECK_HEADERS([gd.h])
fi

# Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_STDBOOL
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_STRUCT_TM

# Checks for library functions.
AC_FUNC_FSEEKO
AC_FUNC_LSTAT
AC_FUNC_LSTAT_FOLLOWS_SLASHED_SYMLINK
AC_FUNC_MALLOC
AC_FUNC_MKTIME
AC_FUNC_REALLOC
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([memchr memmove memset strcasecmp strchr strdup strerror strpbrk strrchr strstr strtol])

# The following lines adds the --enable-pst-debug option to configure:
#
# Give the user the choice to enter one of these:
# --enable-pst-debug
# --enable-pst-debug=yes
# --enable-pst-debug=no
#
AC_MSG_CHECKING([whether we are forcing debug dump file creation])
AC_ARG_ENABLE(pst-debug,
      AC_HELP_STRING([--enable-pst-debug], [force debug dump file creation]),
      [if test "${enable_pst_debug}" = "no" ; then
          AC_MSG_RESULT([no])
      else
          AC_DEFINE(DEBUG_ALL, 1, Define to 1 to force debug dump file creation)
          AC_MSG_RESULT([yes])
      fi],
      # Default value for configure
      AC_MSG_RESULT([no])
      )


AC_OUTPUT(                  \
    Makefile                \
    debian/changelog        \
    debian/Makefile         \
    libpst.spec             \
    html/Makefile           \
    info/Makefile           \
    man/Makefile            \
    src/Makefile            \
    src/version.h           \
    src/pst2dii.cpp 	    \
    xml/Makefile            \
    xml/libpst              \
    )
